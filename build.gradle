plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

def minecraftVersion = stonecutter.current.version
def accesswidener = "sast_login.${minecraftVersion}.accesswidener"

base {
	archivesName = "${project.archives_base_name}-mc${minecraftVersion}"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

loom {
    accessWidenerPath = getRootProject().file("src/main/resources/accesswideners/${accesswidener}")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// JWT verification library used to validate tokens issued by the Egg.js server
	modImplementation "com.auth0:java-jwt:4.4.0"
	include "com.auth0:java-jwt:4.4.0"
	modImplementation "com.fasterxml.jackson.core:jackson-databind:2.14.2"
	include "com.fasterxml.jackson.core:jackson-databind:2.14.2"
	modImplementation "com.fasterxml.jackson.core:jackson-core:2.14.2"
	include "com.fasterxml.jackson.core:jackson-core:2.14.2"
	modImplementation "com.fasterxml.jackson.core:jackson-annotations:2.14.2"
	include "com.fasterxml.jackson.core:jackson-annotations:2.14.2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand([
				version: inputs.properties.version,
				aw_file: accesswidener,
				minecraft_version: minecraftVersion
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.register("collectJars", Copy) {
	dependsOn build

	from tasks.named("remapJar").flatMap { it.archiveFile }

	into rootProject.layout.buildDirectory.dir("libs")
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = base.archivesName
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
